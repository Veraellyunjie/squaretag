#!/usr/bin/perl

use strict;
use warnings;
use Pod::Usage qw(pod2usage);
use Scalar::Util qw(looks_like_number);
use Getopt::Long
  qw(GetOptionsFromArray :config bundling no_ignore_case no_getopt_compat require_order);

( my $app = $0 ) =~ s;.*/;;;

if ( !caller ) {
    run(@ARGV);
    exit 0;
}

sub run {

    my %options;

    my $mode = shift;

    my @rename_opts = qw(verbose|v! dry-run|n! force|f!);

    my %modes = (
        add => {
            cmd      => sub { rename_files( shift, add_tags(@_) ) },
            minargs  => 2,
            opt_spec => \@rename_opts,
            check => [ \&is_tag_file, \&check_tags ],
        },
        remove => {
            cmd     => sub { rename_files( shift, remove_tags(@_) ) },
            minargs => 2,
            opt_spec => \@rename_opts,
            check    => [ \&is_tag_file, \&check_tags ],
            alias    => [ 'rm', 'delete', 'del' ],
        },
        rename => {
            cmd      => sub { rename_files( shift, rename_tag(@_) ) },
            minargs  => 3,
            opt_spec => \@rename_opts,
            check =>
              [ \&is_tag_file, sub { check_tags( shift, "$_[0],$_[1]" ) } ],
            alias => ['mv'],
        },
        clear => {
            cmd      => sub { rename_files( shift, clear(@_) ) },
            minargs  => 1,
            opt_spec => \@rename_opts,
        },
        search => {
            cmd => sub { shift; print $_, "\n" for @{ search_tags(@_) } },
            minargs => 2
        },
        untagged => {
            cmd => sub { shift; print $_, "\n" for @{ find_untagged(@_) } },
            minargs => 1
        },
        list => {
            cmd     => sub { print_tag_list( shift, list_tags(@_) ) },
            minargs => 1
        },
    );

    for my $mode ( values %modes ) {
        if ( my $aliases = $mode->{alias} ) {
            for my $alias (@$aliases) {
                if ( !exists $modes{$alias} ) {
                    $modes{$alias} = $mode;
                }
            }
        }
    }

    if ( $mode && $modes{$mode} ) {
        my $cmd     = $modes{$mode}->{cmd};
        my $minargs = $modes{$mode}->{minargs};

        if ( defined $minargs && @_ < $minargs ) {
            pod2usage(1);
        }

        my $opt_spec = $modes{$mode}->{opt_spec};

        if ($opt_spec) {
            GetOptionsFromArray( \@_, \%options, @$opt_spec )
              or pod2usage(1);
        }

        if ( my $checks = $modes{$mode}->{check} ) {
            for my $check (@$checks) {
                $check->( \%options, @_ );
            }
        }

        $cmd->( \%options, @_ );
    }
    else {
        pod2usage(1);
    }
    return;
}

sub add_tags    { modify( shift, '+', @_ ) }
sub remove_tags { modify( shift, '-', @_ ) }

sub is_tag_file {
    my ( $options, $tag ) = @_;
    pod2usage( -msg => 'Tag is also a file.', -exit => 1 )
      if -e $tag && !$options->{force};
}

sub check_tags {
    my ( $options, $tags ) = @_;
    for my $tag ( split( /,/, $tags ) ) {
        next if $tag =~ /^\s*$/;
        if ( $tag !~ /^\w+(?:=.+)?[+-]?$/ ) {
            pod2usage( -msg => "Invalid tag $tag.", -exit => 1 );
        }
    }
    return;
}

sub print_tag_list {
    my ( $options, $tags ) = @_;
    print $_, " ", $tags->{$_}, "\n" for sort keys %$tags;
}

sub rename_files {
    my ( $options, $ops ) = @_;
    for my $op (@$ops) {
        my ( $old, $new ) = @$op;
        next if $old eq $new;
        if ( !-e $old ) {
            warn "Skip missing file $old.\n";
            next;
        }
        if ( -e $new ) {
            warn "Skip moving $old to $new: File already exists.\n";
            next;
        }
        if ( $options->{'dry-run'} || $options->{verbose} ) {
            print "$old -> $new\n";
            next if $options->{'dry-run'};
        }
        rename $old, $new
          or warn "Can't move $old to $new: $!\n";
    }
    return;
}

sub modify {
    my ( $tag_spec, $default, @files ) = @_;
    my ( @add, @remove );

    $default ||= '+';

    for ( split( /,/, $tag_spec ) ) {
        next if /^\s*$/;
        if (/^(.+)-$/) {
            push @remove, $1;
        }
        elsif (/^(.+)\+$/) {
            push @add, $1;
        }
        elsif ( $default eq '+' ) {
            push @add, $_;
        }
        elsif ( $default eq '-' ) {
            push @remove, $_;
        }
    }

    if ( !@add && !@remove ) {
        return [];
    }

    my @ops;

  FILE:
    for my $file (@files) {
        my ( $base, $tags, $ext ) = split_file($file);

        my @tags = split( /,/, $tags );

        if ( !@tags and !@add ) {
            next FILE;
        }

        push @tags, @add;
        my %tags;
        for my $tag (@tags) {
            my ( $key, $val ) = split( /=/, $tag, 2 );
            $tags{$key} = $val;
        }

        for my $tag (@remove) {
            my ( $key, $val ) = split( /=/, $tag, 2 );
            if ( defined $val ) {
                if ( defined $tags{$key} and $tags{$key} eq $val ) {
                    delete $tags{$key};
                }
            }
            delete $tags{$tag};
        }
        @tags = sort keys %tags;

        my $new_file;
        if (@tags) {
            @tags = map { defined $tags{$_} ? "$_=$tags{$_}" : $_ } @tags;
            $new_file = "$base\[" . join( ',', @tags ) . "\]$ext";
        }
        else {
            $new_file = "$base$ext";
        }

        next if $file eq $new_file;

        push @ops, [ $file, $new_file ];
    }
    return \@ops;
}

sub clear {
    my (@files) = @_;
    my @ops;
    for my $file (@files) {
        my ( $base, $tags, $ext ) = split_file($file);
        my $new_file = "$base$ext";
        push @ops, [ $file, $new_file ];
    }
    return \@ops;
}

sub find_untagged {
    my @files = @_;
    my @found;
    for my $file (@files) {
        my ( $base, $tags, $ext ) = split_file($file);
        if ( !$tags ) {
            push @found, $file;
        }
    }
    return \@found;
}

sub search_tags {
    my ( $tag_spec, @files ) = @_;
    my @found;
  FILE:
    for my $file (@files) {
        my ( $base, $tags, $ext ) = split_file($file);
        my @tags = split( /,/, $tags );

        my %tags;
        for my $tag (@tags) {
            my ( $key, $val ) = split( /=/, $tag, 2 );
            $tags{$key} = $val;
        }
        my $check = compile_search($tag_spec);
        push @found, $file if $check->( \%tags );
    }
    return \@found;
}

sub compile_search {
    my $search = shift;
    my @terms  = split( /\s+|(?=&&|\|\||\(|\)|!|=|<|>)|\b/, $search );
    my $code   = 'sub { my $tags = shift;';
    while ( my $term = shift @terms ) {
        next if $term =~ /^s*$/;
        if ( $term =~ /^\w+$/ ) {
            if ( defined $terms[0] && $terms[0] =~ /^=|<|>$/ ) {
                my ( $op, $operand ) = splice( @terms, 0, 2 );
                $op = $op eq '=' ? 'eq' : $op;
                if ( !defined $operand || $operand !~ /^\w+$/ ) {
                    die "No operand on right side of $term.\n";
                }
                if ( !looks_like_number($operand) ) {
                    if ( $op eq '<' or $op eq '>' ) {
                        die
"Operand $operand isn't numeric in numerical comparison.\n";
                    }
                    $operand = qq{"$operand"};
                }
                $code .= qq{ defined \$tags->{$term} && }
                  . qq{\$tags->{$term} $op $operand};
            }
            else {
                $code .= " exists \$tags->{$term}";
            }
        }
        elsif ( $term =~ /^&&|\|\||\(|\)|!$/ ) {
            $code .= " $term";
        }
        else {
            die "Unknown term $term in search.\n";
        }
    }
    $code .= '}';
    my $sub = eval $code;
    die "$@" if "$@";
    return $sub;
}

sub list_tags {
    my (@files) = @_;
    my %tags;
    for my $file (@files) {
        my ( $base, $tags, $ext ) = split_file($file);
        my @tags = split( /,/, $tags );
        $tags{$_}++ for @tags;
    }
    return \%tags;
}

sub rename_tag {
    my ( $old, $new, @files ) = @_;
    return [] if $old eq $new;
    my @ops;
  FILE:
    for my $file (@files) {
        my ( $base, $tags, $ext ) = split_file($file);
        my @tags = split( /,/, $tags );
        my %tags = map { $_ => 1 } @tags;
        if ( exists $tags{$old} ) {
            delete $tags{$old};
            $tags{$new} = 1;
            @tags = sort keys %tags;
            my $new_file = "$base\[" . join( ',', @tags ) . "\]$ext";
            push @ops, [ $file, $new_file ];
        }
    }
    return \@ops;
}

sub split_file {
    my $file = shift;
    my ( $base, $tags, $ext );
    ( $base, $ext )  = $file =~ m/^(.+?)(\.[^.]*$|$)/;
    ( $base, $tags ) = $base =~ m/^(.*?)(?:\[([^[]*)\]$|$)/;
    if ( !$base ) {
        $base = "[$tags]";
        $tags = '';
    }
    $tags //= '';
    return ( $base, $tags, $ext );
}

1;

__END__

=for html <a href="https://travis-ci.org/mdom/squaretag"><img src="https://travis-ci.org/mdom/squaretag.svg?branch=master"></a>  <a href='https://coveralls.io/r/mdom/squaretag?branch=master'><img src='https://coveralls.io/repos/mdom/squaretag/badge.png?branch=master' alt='Coverage Status' /></a>

=head1 NAME

squaretag - tag files by using filenames

=head1 SYNOPSIS

  squaretag add    [--verbose|-v] [--dry-run|-n] [--force|-f] TAG FILES...
  squaretag remove [--verbose|-v] [--dry-run|-n] [--force|-f] TAG FILES...
  squaretag clear  [--verbose|-v] [--dry-run|-n] [--force|-f] FILES...
  squaretag rename [--verbose|-v] [--dry-run|-n] [--force|-f] OLD_TAG NEW_TAG FILES...
  squaretag list FILES...
  squaretag search SEARCH FILES...
  squaretag untagged FILES...

=head1 DESCRIPTION

Squaretag helps you to organize your files by tagging them. The tags
are saved in the filename by adding all tags in square brackets either
at the end of the filename or just before the last dot.

=head1 EXAMPLES

Suppose the current directory contains the following files:

  $ squaretag untagged
  picture01.jpg
  picture02

Now you want to add the tag I<family> to all pictures.

  $ squaretag add -v family *
  picture01.jpg -> picture01[family].jpg
  picture02 -> picture02[family]

Files can have multiple tags

  $ squaretag add -v myself picture01[family].jpg
  picture01[family].jpg -> picture01[family,myself].jpg

You can rename single tags

  $ squaretag rename -v myself me picture01[family,myself].jpg
  picture01[family,myself].jpg -> picture01[family,me].jpg

Or delete a tag

  $ squaretag remove me -v picture01[family,me].jpg
  picture01[family,me].jpg -> picture01[family].jpg

And what would tags be good for if you couldn't search for them

  $ squaretag search 'family && !me' *
  picture02[family]

And if you have enough, you can simple clear all tags

  $ squaretag clear *
  picture01[family].jpg -> picture01.jpg
  picture02[family] -> picture02

Tags can also have values:

  $ squaretag add read=2016 book.epub
  $ squaretag search 'read>2010' *
  books[read=2016].epub
  $ squaretag search 'read<2010' *
  $ squaretag search 'read=2016' *
  books[read=2016].epub

=head1 COMMANDS

=over 4

=item add TAG FILES...

Add TAG to FILES. TAG is a comma seperated list of tags to add. If
a tag has a suffix of I<-> this tag will be removed from the file.

=item remove TAG FILES...

Remove TAG from FILES. TAG is a comma seperated list of tags to remove. If
a tag has a suffix of I<+> this tag will be added to the file.

=item search SPEC FILES...

List all FILES that match the search SPEC. SPEC can contain tags and
the usual boolean operators.

For example:

  read && good
  !read || (not_read && prio )

There are three additional operators I<<>, I<=> and I<>> to handle tags
with values. The I<=> compares its operand as strings, the other two
just work on numerical tags.

=item clear FILES...

Remove all tags from FILES.

=item rename OLD_TAG NEW_TAG FILES...

Rename OLD_TAG in NEW_TAG in all FILES.

=item list FILES...

List and count all tags for FILES. The tags are sorted in descending
order of frequency.

=item untagged FILES...

List all FILES without a tag.

=back

=head1 RATIONALE

Tagging files in a way that tags are preserved cross-platform or even
cross-filesystem seems to be a hard problem with no clear cut solution.

=head2 Fileformat

Some file formats as mp3 or pdf support saving tags inside the files but there
a a lot (probably most) that don't. And even the former need special programs
to do that.

=head2 Filesystem

Many modern file systems support tags and special meta data streams, but the
tags are most likely lost when you copy files from one file system to the next.

=head2 External database

Keeping the tags in a external database requires a sync step after moving or
deleting files. This is harder than it sounds, you probably need the keep
checksums to reckognize known files after a rename.

=head2 Filename

All filesystems limit the length of the filename, so the number of tags
you can save per file is also limited. And even before you reach the
limit, the filename will be hard to read.

But at least it works for all kinds of files, it not dependent on your file or
operating sytemen is does not need external tools or syncing steps.

=head1 SEE ALSO

L<tmsu|http://tmsu.org/>, L<tagspaces|https://www.tagspaces.org/>

=head1 COPYRIGHT AND LICENSE

Copyright 2016 Mario Domgoergen C<< <mario@domgoergen.com> >>

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

=cut
